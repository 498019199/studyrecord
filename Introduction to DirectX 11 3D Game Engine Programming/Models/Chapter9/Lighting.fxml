<?xml version='1.0'?>

<effect>
    <struct name="DirectionalLight">
        <member type="float4" name="Ambient"/>
        <member type="float4" name="Diffuse"/>
        <member type="float4" name="Specular"/>
        <member type="float3" name="Direction"/>
        <member type="float" name="pad"/>
    </struct>
        <struct name="PointLight">
        <member type="float4" name="Ambient"/>
        <member type="float4" name="Diffuse"/>
        <member type="float4" name="Specular"/>
        <member type="float3" name="Position"/>
        <member type="float" name="Range"/>
        <member type="float3" name="Att"/>
        <member type="float" name="pad"/>
    </struct>
        <struct name="SpotLight">
        <member type="float4" name="Ambient"/>
        <member type="float4" name="Diffuse"/>
        <member type="float4" name="Specular"/>
        <member type="float3" name="Position"/>
        <member type="float" name="Range"/>
        <member type="float3" name="Direction"/>
        <member type="float" name="Spot"/>
        <member type="float3" name="Att"/>
        <member type="float" name="pad"/>
    </struct>
    <struct name="Material">
        <member type="float4" name="Ambient"/>
        <member type="float4" name="Diffuse"/>
        <member type="float4" name="Specular"/>
        <member type="float4" name="Reflect"/>
    </struct>
	<shader>
		<![CDATA[
void ComputeDirectionalLight(Material mat, 
	DirectionalLight L,
    float3 normal, 
	float3 toEye,
    out float4 ambient,
    out float4 diffuse,
    out float4 spec)
{
    // 初始化输出
    ambient = float4(0.0f, 0.0f, 0.0f, 0.0f);
    diffuse = float4(0.0f, 0.0f, 0.0f, 0.0f);
    spec = float4(0.0f, 0.0f, 0.0f, 0.0f);

	// 光照矢量与光线的传播方向相反
	float3 lightVec = -L.Direction;
	// 添加环境光
	ambient = mat.Ambient * L.Ambient;	
	// 添加漫反射和镜面光
	float diffuseFactor = dot(lightVec, normal);

	// Flatten避免动态分支
	[flatten]
	if( diffuseFactor > 0.0f )
	{
		float3 v         = reflect(-lightVec, normal);
		float specFactor = pow(max(dot(v, toEye), 0.0f), mat.Specular.w);
					
		diffuse = diffuseFactor * mat.Diffuse * L.Diffuse;
		spec    = specFactor * mat.Specular * L.Specular;
	}
}

void ComputePointLight(Material mat, 
	PointLight L, 
	float3 pos, 
	float3 normal, 
	float3 toEye,
    out float4 ambient, 
	out float4 diffuse, 
	out float4 spec)
{
	// 初始化输出
    ambient = float4(0.0f, 0.0f, 0.0f, 0.0f);
    diffuse = float4(0.0f, 0.0f, 0.0f, 0.0f);
    spec = float4(0.0f, 0.0f, 0.0f, 0.0f);

	// 表面指向光源的矢量
	float3 lightVec = L.Position - pos;
		
	// 表面离光源的距离
	float d = length(lightVec);
	
	// Range test.
	if( d > L.Range )
		return;

	// Normalize the light vector.
	lightVec /= d; 

	// Ambient term.
	ambient = mat.Ambient * L.Ambient;

	// Add diffuse and specular term, provided the surface is in 
	// the line of site of the light.
	float diffuseFactor = dot(lightVec, normal);

	// Flatten to avoid dynamic branching.
	[flatten]
	if( diffuseFactor > 0.0f )
	{
		float3 v         = reflect(-lightVec, normal);
		float specFactor = pow(max(dot(v, toEye), 0.0f), mat.Specular.w);
					
		diffuse = diffuseFactor * mat.Diffuse * L.Diffuse;
		spec    = specFactor * mat.Specular * L.Specular;
	}

	// 衰减
	float att = 1.0f / dot(L.Att, float3(1.0f, d, d*d));
	diffuse *= att;
	spec    *= att;
}

void ComputeSpotLight(Material mat, 
	SpotLight L, 
	float3 pos, 
	float3 normal, 
	float3 toEye,
    out float4 ambient, 
	out float4 diffuse, 
	out float4 spec)
{
	// 初始化输出
    ambient = float4(0.0f, 0.0f, 0.0f, 0.0f);
    diffuse = float4(0.0f, 0.0f, 0.0f, 0.0f);
    spec = float4(0.0f, 0.0f, 0.0f, 0.0f);

	// 从表面指向光源的光照矢量
	float3 lightVec = L.Position - pos;
		
	// 表面离开光源的距离
	float d = length(lightVec);
	
	// Range test.
	if( d > L.Range )
		return;

	// 规范化光照矢量
	lightVec /= d; 
	
	// 计算环境光
	ambient = mat.Ambient * L.Ambient;	

	// 计算漫反射和镜面光，provided the surface is in 
	// the line of site of the light.
	float diffuseFactor = dot(lightVec, normal);

	// Flatten避免动态分支
	[flatten]
	if( diffuseFactor > 0.0f )
	{
		float3 v         = reflect(-lightVec, normal);
		float specFactor = pow(max(dot(v, toEye), 0.0f), mat.Specular.w);
					
		diffuse = diffuseFactor * mat.Diffuse * L.Diffuse;
		spec    = specFactor * mat.Specular * L.Specular;
	}
	
	// Scale by spotlight factor and attenuate.
	float spot = pow(max(dot(-lightVec, L.Direction), 0.0f), L.Spot);

	// Scale by spotlight factor and attenuate.
	float att = spot / dot(L.Att, float3(1.0f, d, d*d));

	ambient *= spot;
	diffuse *= att;
	spec    *= att;
}
		]]>
	</shader>
</effect>
